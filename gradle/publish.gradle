// Publish gh-pages on github
apply plugin: "org.ajoberstar.github-pages"

ext {
    githubApiKey = System.getenv("GH_TOKEN")
    repoName = rootProject.name
}

apply plugin: "maven-publish"
def nexusUser = System.getenv("NEXUS_USER") ?: project.cgoitNexusUsername
def nexusPassword = System.getenv("NEXUS_PASSWORD") ?: project.cgoitNexusPassword
def nexusBase = System.getenv("NEXUS_BASE") ?: project.cgoitNexusBase
publishing.repositories {
    maven {
        credentials {
            username "${nexusUser}"
            password "${nexusPassword}"
        }

        url = "$nexusBase/repository/grails-plugins"
    }
}

publishing.publications {
    grailsElasticsearchPlugin(MavenPublication) {
        afterEvaluate {
            artifactId = project.name
            version project.version
            from components.java
            artifact sourcesJar
            pom {
                description = """Elasticsearch is a search server based on Lucene. It provides a distributed, multitenant-capable 
full-text search engine with an HTTP web interface and schema-free JSON documents. Elasticsearch is developed in 
Java and is released as open source under the terms of the Apache License. 
This is the Grails 3 plugin to support Elasticsearch up to Version 7.7.1."""
            }
        }
    }
}

publish.dependsOn = [sourcesJar, docs]

githubPages {
    repoUri = 'https://github.com/cgoIT/elasticsearch-grails-plugin.git'

    credentials {
        username = project.hasProperty('githubApiKey') ? project.githubApiKey : ''
        password = ''
    }

    pages {
        from "${buildDir}/docs"
    }
}

task publishDocs(dependsOn: [docs, publishGhPages])